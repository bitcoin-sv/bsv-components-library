plugins {
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

/****************************************
 * Local Configuration
 ****************************************/
jclGroup = "$jclGroup"
jclVersion = "$jclVersion"
jclArtifactName = "jcl-store-foundationDB"

group = "$rootProject.jclGroup"
version = "$rootProject.jclVersion"
sourceCompatibility = "11"
targetCompatibility = "11"

dependencies {
    // Compile Dependencies:
    implementation "io.bitcoinsv.bitcoinjsv:bitcoinj-base:$bitcoinJVersion"
    implementation(project(":tools"))
    implementation(project(":store"))
    implementation(project(":store-keyValue"))
    implementation 'org.foundationdb:fdb-java:6.2.22'
    implementation 'org.slf4j:slf4j-api:1.7.26'

    testImplementation(project(":store-tck"))
    testImplementation 'org.codehaus.groovy:groovy'
    testImplementation "org.spockframework:spock-core:1.2-groovy-2.5"
    testImplementation "ch.qos.logback:logback-classic"

    // TestContainer, to start automatically a FoundationDB running in a Docker container...
    // NOTE: Not used at the moment, since starting and stopping the container is NOT enough: We also need to
    // INITIALIZE the DataBase, meaning we need to run some Shell Scripts. So instead of using TestContainers, we
    // are just executing some Shell Scripts Before and After the Tests (check DockerTestUtils class).

    testImplementation 'org.testcontainers:spock:1.15.1'

}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava11Compatible()) {
        options.addBooleanOption('Xdoclint:none', true)
    }
}

/****************************************
 * Publish the JAR
 ****************************************/
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "$jclArtifactName"
            groupId = "$jclGroup"
            version = "$jclVersion"

            from components.java

            pom {
                name = "$jclArtifactName"
                description = 'Java implementation of Bitcoin components'
                url = 'https://github.com/bitcoin-sv/bsv-components-library'

                licenses {
                    license {
                        name = 'Open BSV License'
                        url = 'https://github.com/bitcoin-sv/bsv-components-library/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        organization = 'nChain'
                        organizationUrl = 'https://nchain.com'
                        name = 'i.fernandez'
                    }
                }
                scm {
                    url = 'https://github.com/bitcoin-sv/bsv-components-library'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}


jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
    dependsOn test
}

test {
    finalizedBy jacocoTestReport
}
